# Maintainer: Slashbunny <demodevil5[at]yahoo>

pkgname=prometheus-bin
pkgver=2.13.0
pkgrel=1
pkgdesc="An open-source service monitoring system and time series database (binary, not built from source)"
arch=('x86_64')
url="http://prometheus.io"
license=('Apache')
provides=('prometheus')
conflicts=('prometheus' 'prometheus-git')
backup=('etc/prometheus/prometheus.yml' 'etc/prometheus/cli.config')
source=('prometheus.service' 'cli.config'
    "https://github.com/prometheus/prometheus/releases/download/v${pkgver}/prometheus-${pkgver}.linux-amd64.tar.gz" )
sha256sums=('3461430433f75e3c083244003399c661e8f71c252013e4f0508c31ac33c7380a'
    '77a087931de4e397e58da2eea9f54c61add54d04411d352876deb6befefd8fb0'
    'af9dea0a130b1b5eddf9f5c2d186e19e0b5493f6d8dde5aa897b46c7437942c7')

package() {
    cd "${srcdir}/prometheus-${pkgver}.linux-amd64"

    # Install Binaries
    install -D -m0755 prometheus \
        "${pkgdir}/usr/bin/prometheus"

    install -D -m0755 promtool \
        "${pkgdir}/usr/bin/promtool"

    # Install Config File
    install -D -m0755 prometheus.yml \
        "${pkgdir}/etc/prometheus/prometheus.yml"
    
    install -D -m0755 "${srcdir}/cli.config" \
        "${pkgdir}/etc/prometheus/cli.config"

    # Install SystemD Service File
    install -D -m0644 "${srcdir}/prometheus.service" \
        "${pkgdir}/usr/lib/systemd/system/prometheus.service"

    # Install Console files
    cp -R consoles/ \
        "${pkgdir}/etc/prometheus/consoles"

    cp -R console_libraries/ \
        "${pkgdir}/etc/prometheus/console_libraries"
}
